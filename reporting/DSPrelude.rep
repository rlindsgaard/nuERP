isMe : <Agent> -> Bool
isMe a = a :? <Me>

-- Product normalised list of all items given in list
normaliseItems : [Item] -> [Item]
normaliseItems its = [Item{productType = i.1} |
  i <- groupProj (\is -> is.productType) its]

invoices : [(Int, IssueInvoice)]
invoices = [(tr.contractId,inv) |
  tr <- transactionEvents,
  inv : IssueInvoice = tr.transaction]

invoicesSent : [(Int, IssueInvoice)]
invoicesSent = filter (\inv -> isMe inv.2.sender && not (isMe inv.2.receiver)) invoices

itemsReceived : [Item]
itemsReceived = normaliseItems [is |
  tr <- transactionEvents,
  del : Delivery = tr.transaction,
  not(isMe del.sender) && isMe del.receiver,
  lines <- del.orderLines,
  is <- lines.items]

itemsSold : [Item]
itemsSold = normaliseItems [line.item | inv <- invoicesSent, line <- inv.2.orderLines]

deliveries : [Delivery]
deliveries = [del |
  tr <- transactionEvents,
  del : Delivery = tr.transaction]

itemStock : [[OrderLine]] -> [Stock]
itemStock olss = [Stock{item = head [l.item | l <- ols], quantity = sum [l.quantity | l <- ols]} | 
  ols <- olss]

incomming : [Stock]
incomming = itemStock (groupBy (\x y -> x.item == y.item) (sort [ls |
  del <- deliveries,
  not(isMe del.sender) && isMe del.receiver,
  ls <- del.orderLines]))

outgoing : [Stock]
outgoing = itemStock (groupBy (\x y -> x.item == y.item) (sort [ls |
  del <- deliveries,
  isMe del.sender && not(isMe del.receiver),
  ls <- del.orderLines]))
